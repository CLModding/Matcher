plugins {
	id 'java-library'
	id 'application'
	id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
	id 'org.openjfx.javafxplugin' version '0.0.10'
	id 'de.jjohannes.extra-java-module-info' version '0.5'
}

repositories {
	mavenCentral()
	maven {
		name "FabricMc"
		url "https://maven.fabricmc.net/"
	}
	maven {
		name "Quilt"
		url 'https://maven.quiltmc.org/repository/release/'
	}
}

archivesBaseName = 'matcher'
group = 'org.quiltmc'

sourceSets {
	main {
		java
		resources
	}
}

javafx {
	version = "18"
	modules = [ 'javafx.controls', 'javafx.web' ]
}

java {
	modularity.inferModulePath = true
	sourceCompatibility = JavaVersion.VERSION_18
	targetCompatibility = JavaVersion.VERSION_18

	if (!JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_18)) {
		toolchain {
			languageVersion = JavaLanguageVersion.of(18)
		}
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 18
}

dependencies {
	api "org.ow2.asm:asm:9.3"
	api "org.ow2.asm:asm-tree:9.3"
	implementation "org.ow2.asm:asm-commons:9.3"
	implementation "org.ow2.asm:asm-util:9.3"

	implementation "org.jfxtras:jmetro:11.6.15"

	implementation "com.github.javaparser:javaparser-core:3.24.2"

	implementation "org.quiltmc:procyon-quilt-compilertools:0.5.35.local"
	implementation "org.quiltmc:cfr:0.0.6"
	implementation "org.quiltmc:quiltflower:1.8.1"

	implementation "net.fabricmc:mapping-io:0.3.0"
}

// TODO: Add Automatic-Module-Name to all these in Quilt forks
extraJavaModuleInfo {
	automaticModule("cfr-0.0.6.jar", "cfr")
	automaticModule("procyon-quilt-compilertools-0.5.35.local.jar", "procyon.compilertools")
	automaticModule("procyon-quilt-core-0.5.35.local.jar", "procyon.core")
	automaticModule("quiltflower-1.8.1.jar", "intellij.fernflower")
}

application {
	mainModule = 'matcher'
	mainClass = 'matcher.Main'
}

build.dependsOn(shadowJar)

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}